/** Implements the Lunar Lander game. */

class ShuttleGame {
	field Shuttle shuttle; // the shuttle used in the game

	/** Constructs a new Shuttle Game. */
	constructor ShuttleGame new() {
		let shuttle = Shuttle.new(16);
		return this;
	}

	/** Disposes this game. */
	method void dispose() {
		do shuttle.dispose();
		do Memory.deAlloc(this);
		return;
	}

	method void createBullet() {
		var Bullet bullet;
		let bullet = Bullet.new(shuttle.getLocation() - 512);
		do bullet.animate();
		return;
	}

	/** Runs the game. */
	method void run() {
		var char key;
		var boolean exit;
		let exit = false;

		while (~exit) {
			// wait for a key to be pressed
			while (key = 0) {
				let key = Keyboard.keyPressed();
				do shuttle.shuttleGravity();
				do Sys.wait(5);
			}

			if (key = 81) { let exit = true; }		// q key

			if (key = 131) { 						// up arrow
				while (key = 131) {
					let key = Keyboard.keyPressed();
					do shuttle.moveUp();
					do Sys.wait(10);
				}
			}

			if (key = 130) { 						// left arrow
				while (key = 130) {
					let key = Keyboard.keyPressed();
					do shuttle.moveLeft();
					do Sys.wait(50);
				}
			}

			if (key = 132) { 						// right arrow
				while (key = 132) {
					let key = Keyboard.keyPressed();
					do shuttle.moveRight();
					do Sys.wait(50);
				}
			}

			if (key = 129) {						// backspace
				while (key = 129) {
					let key = Keyboard.keyPressed();
					do createBullet();
				}
			}
		} // endwhile
		return;
	}
}