/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    // 8-stack of RAM64
    // 6 chars needed in address
    RAM64(in=in, load=ramLoad0, address=address[3..8], out=ramOut0);
    RAM64(in=in, load=ramLoad1, address=address[3..8], out=ramOut1);
    RAM64(in=in, load=ramLoad2, address=address[3..8], out=ramOut2);
    RAM64(in=in, load=ramLoad3, address=address[3..8], out=ramOut3);
    RAM64(in=in, load=ramLoad4, address=address[3..8], out=ramOut4);
    RAM64(in=in, load=ramLoad5, address=address[3..8], out=ramOut5);
    RAM64(in=in, load=ramLoad6, address=address[3..8], out=ramOut6);
    RAM64(in=in, load=ramLoad7, address=address[3..8], out=ramOut7);

    // send load signal to 8-stack with 8-way demultiplexor
    // 3 chars needed in address
    DMux8Way(
    	in=load,
    	sel=address[0..2],
    	a=ramLoad0,
    	b=ramLoad1,
    	c=ramLoad2,
    	d=ramLoad3,
    	e=ramLoad4,
    	f=ramLoad5,
    	g=ramLoad6,
    	h=ramLoad7
    );

    // select which of the 8 RAM64s to send to out
    // 3 chars needed in address
    Mux8Way16(
    	sel=address[0..2],
    	a=ramOut0,
    	b=ramOut1,
    	c=ramOut2,
    	d=ramOut3,
    	e=ramOut4,
    	f=ramOut5,
    	g=ramOut6,
    	h=ramOut7,
    	out=out
    );
}