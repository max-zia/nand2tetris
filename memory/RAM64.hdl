// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl

/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // 8-stack of RAM8
    RAM8(in=in, load=ramLoad0, address=address[3..5], out=ramOut0);
    RAM8(in=in, load=ramLoad1, address=address[3..5], out=ramOut1);
    RAM8(in=in, load=ramLoad2, address=address[3..5], out=ramOut2);
    RAM8(in=in, load=ramLoad3, address=address[3..5], out=ramOut3);
    RAM8(in=in, load=ramLoad4, address=address[3..5], out=ramOut4);
    RAM8(in=in, load=ramLoad5, address=address[3..5], out=ramOut5);
    RAM8(in=in, load=ramLoad6, address=address[3..5], out=ramOut6);
    RAM8(in=in, load=ramLoad7, address=address[3..5], out=ramOut7);

    // send load signal to RAM8 stack
    DMux8Way(
    	in=load, 
    	sel=address[0..2], 
    	a=ramLoad0,
    	b=ramLoad1,
    	c=ramLoad2,
    	d=ramLoad3,
    	e=ramLoad4,
    	f=ramLoad5,
    	g=ramLoad6,
    	h=ramLoad7
    );

    // select RAM8 to output 
    Mux8Way16(
    	a=ramOut0,
    	b=ramOut1,
    	c=ramOut2,
    	d=ramOut3,
    	e=ramOut4,
    	f=ramOut5,
    	g=ramOut6,
    	h=ramOut7,
    	sel=address[0..2],
    	out=out
    );

}